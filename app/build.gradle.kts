plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
}

android {
    namespace = "com.volumemanager.app"
    compileSdk = 35

    defaultConfig {
        applicationId = "com.volumemanager.app"
        minSdk = 33
        targetSdk = 35
        versionCode = 1
        versionName = "1.0.0"
    }

    flavorDimensions += "backend"
    productFlavors {
        create("root") {
            dimension = "backend"
        }
        create("sui") {
            dimension = "backend"
            applicationIdSuffix = ".sui"
            versionNameSuffix = "-sui"
        }
    }

    buildTypes {
        release {
            isMinifyEnabled = true
            isShrinkResources = true
            // Sideload / GitHub Releases: debug keystore until a dedicated release key exists.
            signingConfig = signingConfigs.getByName("debug")
            proguardFiles(
                getDefaultProguardFile("proguard-android-optimize.txt"),
                "proguard-rules.pro",
            )
        }
        debug {
            isMinifyEnabled = false
            isShrinkResources = false
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = "17"
    }

    buildFeatures {
        aidl = true
        viewBinding = true
        buildConfig = true
    }

    packaging {
        resources {
            excludes += "/META-INF/{AL2.0,LGPL2.1}"
        }
    }
}

dependencies {
    implementation("androidx.core:core-ktx:1.15.0")
    implementation("androidx.appcompat:appcompat:1.7.0")
    implementation("com.google.android.material:material:1.12.0")
    implementation("androidx.constraintlayout:constraintlayout:2.2.0")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:2.8.7")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:1.9.0")

    val libsu = "6.0.0"
    "rootImplementation"("com.github.topjohnwu.libsu:core:$libsu")
    "rootImplementation"("com.github.topjohnwu.libsu:service:$libsu")

    val shizuku = "13.1.5"
    "suiImplementation"("dev.rikka.shizuku:api:$shizuku")
    "suiImplementation"("dev.rikka.shizuku:provider:$shizuku")
}
